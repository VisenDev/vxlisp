(package vx/type
 :doc "Type handling tools.")

(func allowtypenames<-type : stringlist
 [type : any]
 (typenames<-typelist (allowtypes<-type type))
 :doc "Get the name of a given type")

(func allowtypes<-type : typelist
 [type : any]
 (:allowtypes (typedef<-type type))
 :doc "Returns the allowed types from a given type")

(func any<-int : any-1
 [val : int]
 :doc "Generic function returns any from an int.")

(func int<-string-find : int
 [text : string
  find : string]
 (native
  :cpp
   "std::string stext = text->vx_string();
    std::string sfind = find->vx_string();
    int ipos = vx_core::vx_int_from_sizet(stext.find(sfind)) + 1;
    output = vx_core::vx_new_int(ipos);"
  :js
   "text.indexOf(find) + 1"
  :java
   "String stext = text.vx_string();
    String sfind = find.vx_string();
    int ipos = stext.indexOf(sfind) + 1;
    output = Core.vx_new_int(ipos);")
 :test (test
        3
        (int<-string-find
         "abcdefg"
         "cd"))
 :doc "Returns integer position of find string in text string.")

(func int<-string-findkeyword : int
 [text : string
  find : string]
 (native
  :cpp
   "int ipos = vx_type::vx_int_from_string_findkeyword(text->vx_string(), find->vx_string());
    output = vx_core::vx_new_int(ipos);"
  :js
   "vx_type.vx_int_from_string_findkeyword(text, find)"
  :java
   "int ipos = vx_int_from_string_findkeyword(text.vx_string(), find.vx_string());
    output = Core.vx_new_int(ipos);")
 :test (test
        3
        (int<-string-findkeyword
         "ab\tcdefg"
         ":whitespace"))
       (test
        5
        (int<-string-findkeyword
         " \t\n\rab"
         ":nonwhitespace"))
 :doc "Returns integer position of find string in text string. Note: the find terms :whitespace and :nonwhitespace have special meaning.")

(func is-boolean : boolean [value : any]
 (= "boolean" (typename<-any value)))

(func is-decimal : boolean [value : any]
 (= "decimal" (typename<-any value)))

(func is-float : boolean
 [value : any]
 (= "float" (typename<-any value)))

(func is-none : boolean
 [value : any]
 (= value none))

(func is-string : boolean
 [value : any]
 (= "vx/core/string" (typename<-any value))
 :test (test-true
        (is-string ""))
       (test-true
        (is-string "a"))
       (test-false
        (is-string 5))
       (test-false
        (is-string (list))))

(func is-type : boolean
 [val  : any
  type : any]
 (or
  (= (typename<-type type) (typename<-any val))
  (contains (allowtypenames<-type type) (typename<-any val))
  (contains (traitnames<-any val) (typename<-type type)))
 :test (test-true
        (is-type false boolean))
       (test-true
        (is-type "a" string))
       (test-true
        (is-type 5.5 number))
       (test-true
        (is-type 4 int)))

(func is-type<-any-typelist : boolean
 [val      : any
  typelist : typelist]
 (any<-list-reduce : boolean
  typelist
  false
  (fn : boolean
   [result : boolean
    type   : any]
   (or result (is-type val type))))
 :test (test-true
        (is-type<-any-typelist
         false
         (typelist int boolean)))
       (test-true
        (is-type<-any-typelist
         "a"
         (typelist number string)))
       (test-true
        (is-type<-any-typelist
         5.5
         (typelist string number)))
       (test-true
        (is-type<-any-typelist
         4
         (typelist string int)))
 :doc "Returns true if val is any type in typelist.")

(func length<-string : int
 [text : string]
 (native
  :cpp
   "long len = text->vx_string().length();
    output = vx_core::vx_new_int(len);"
  :js
   "text.length"
  :java
   "int len = text.vx_string().length();
    output = Core.vx_new_int(len);")
 :test (test
        4
        (length<-string "abcd"))
       (test
        0
        (length<-string ""))
 :doc "Returns length of a string.")

(func string<-int : string
 [val : int]
 (switch val : string
  (case infinity    "infinity")
  (case neginfinity "neginfinity")
  (case notanumber  "notanumber")
  (else (string val)))
 :test (test
        "4"
        (string<-int 4))
       (test
        "51"
        (string<-int 51))
       (test
        "notanumber"
        (string<-int notanumber))
       (test
        "infinity"
        (string<-int infinity))
       (test
        "neginfinity"
        (string<-int neginfinity))
 :doc "Function Type converting int to string")

(func string<-string-end : string
 [text   : string
  endpos : int]
 (string<-string-start-end
  text
  1
  endpos)
 :test (test
        "abc"
        (string<-string-end
         "abcd"
         3))
       (test
        "ab"
        (string<-string-end
         "abcd"
         2))
 :doc "Returns a substring between 0 end position.")

(func string<-string-start : string
 [text     : string
  startpos : int]
 (string<-string-start-end text startpos (length<-string text))
 :test (test
        "abcd"
        (string<-string-start
         "abcd"
         1))
       (test
        "bcd"
        (string<-string-start
         "abcd"
         2))
 :doc "Returns a substring between start string end.")

(func string<-string-start-end : string
 [text  : string
  start : int
  end   : int]
 (native
  :cpp
   "std::string str = vx_core::vx_string_from_string_start_end(text->vx_string(), start->vx_int(), end->vx_int());
    output = vx_core::vx_new_string(str);"
  :js "vx_core.vx_string_from_string_start_end(text, start, end)"
  :java
   "String stext = Core.vx_string_from_string_start_end(text.vx_string(), start.vx_int(), end.vx_int());
    output = Core.vx_new_string(stext);")
 :test (test
        "abc"
        (string<-string-start-end
         "abcd"
         1
         3))
       (test
        "bc"
        (string<-string-start-end
         "abcd"
         2
         3))
       (test
        ""
        (string<-string-start-end
         "abcd"
         5
         6))
       (test
        "bcd"
        (string<-string-start-end
         "abcd"
         2
         5))
 :doc "Returns a substring between start and end positions.")

(func string<-stringlist-join : string
 [vals  : stringlist
  delim : string]
 (native
  :cpp
   "output = vx_type::vx_string_from_stringlist_join(vals, delim);"
  :js "vals.join(delim)"
  :java
   "output = Type.vx_string_from_stringlist_join(vals, delim);")
 :test (test
        "a$b$c"
        (string<-stringlist-join
         (stringlist "a" "b" "c")
         "$"))
 :doc "Returns a string by joining a stringlist by the delimiter")

(func stringlist<-string-split : stringlist
 [text  : string
  delim : string]
 (native
  :cpp
   "vx_type::vx_stringlist_from_string_split(text, delim);"
  :js "vx_type.vx_stringlist_from_string_split(text, delim)"
  :java
   "Type.vx_stringlist_from_string_split(text, delim);")
 :test (test
        (stringlist "a" "b" "c")
        (stringlist<-string-split
         "a$b$c"
         "$"))
 :doc "Returns a stringlist by splitting a string by the delimiter")

(func traitnames<-any : stringlist
 [val : any]
 (typenames<-typelist
  (traits<-any val))
 :doc "Get the trait names of a given type")

(func traits<-any : typelist
 [val : any]
 (traits<-typedef
  (typedef<-any val))
 :doc "Get the traits of a given value")

(func traits<-typedef : typelist
 [vtypedef : typedef]
 (:traits vtypedef)
 :doc "Get the traits of a given typedef")
