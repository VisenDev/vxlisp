(package vx/repl
 :libs (lib type :path vx/type)
       (lib tb   :path vx/data/textblock)
       (lib javafuture)

 :doc "Read-Evaluate-Print-Loop processor.")

(type liblist : list
 :allowtypes [string]
 :doc "List of library names")

(type repl : struct
 :properties [type     : any
              repllist : repllist
              async    : boolean
              val      : any]
 :doc "Repl structure")

(type repllist : list
 :allowtypes [repl]
 :doc "List of repl")

(const delimvxlisp : delim
 (delim
  :name "delimvxlisp"
  :delimlist
   (delimlist
    delimvxlispparen
    delimcomment
    delimcommentblock))
 :doc "vxlisp File Delimiters")

(const delimvxlispbracket : delim
 (copy delimbracketsquare
  :name "delimvxlispbracketsquare"
  :delimlist
   (delimlist
    delimcomment
    delimcommentblock
    delimquote
    delimquoteblock
    delimwhitespace
    delimvxlispbracket
    delimvxlispparen))
 :doc "vxlisp Square Bracket Delimiters")

(const delimvxlispparen : delim
 (copy delimparen
  :name "delimvxlispparen"
  :delimlist
   (delimlist
    delimcomment
    delimcommentblock
    delimquote
    delimquoteblock
    delimwhitespace
    delimvxlispbracket
    delimvxlispparen))
 :doc "vxlisp Paren Delimiters")

(func any<-liblist-string : any
 [liblist : liblist
  text    : string :doc "Program to run"]
 (let : any
  [repl : repl := (repl<-liblist-string liblist text)]
  (any<-repl repl))
 :context
 :doc   "Run an arbitrary program in the REPL.")

/*
(func any<-liblist-string-async : any
 [liblist : liblist
  text    : string :doc "Program to run"]
 (let : any
  [repl : repl := (repl<-liblist-string liblist text)
   val  : any  := (any<-repl-async repl)]
  val)
 :async
 :context
 :doc   "Run an arbitrary program in the REPL asynchrously.")
*/

(func any-repl<-functype-args : any
 [type : any
  args : anylist]
 (native
  :cpp
   "vx_core::Type_replfunc replfunc = dynamic_cast<vx_core::Type_replfunc>(type);
    output = replfunc->vx_repl(args);"
  :js
   "const vxvalue = type['vx_value']
    if (vxvalue != undefined) {
      const fn = vxvalue['fn']
      if (fn != undefined) {
        output = fn(...args)
      }
    }"
  :java
   "if (type instanceof Core.Type_replfunc) {
      Core.Type_replfunc replfunc = (Core.Type_replfunc)type;
      output = replfunc.vx_repl(args);
    }")
 :doc "Returns any from a repl func and args.")

(func any<-repl : any
 [repl : repl]
 (let : any
  [val      : any      := (:val repl)
   repltype : any      := (:type repl)
   repllist : repllist := (:repllist repl)
   args     : anylist  := (anylist<-repllist repllist)]
  (if
   (then (!-empty val) val)
   (then (is-func repltype)
    (any-repl<-functype-args
     repltype
     args))
   (else
    (new repltype args))))
 :context
 :test (test
        "HelloWorld"
        (any<-repl
         (repl
          :type string
          :repllist
           (repllist
            (repl :val "Hello")
            (repl :val "World")
           ))))
       (test
        5
        (any<-repl
         (repl
          :type +
          :repllist
           (repllist
            (repl :val 2)
            (repl :val 3)
           ))))           
 :doc "Run an arbitrary program in the REPL.")

/*
(func any<-repl-async : any
 [repl : repl]
 (let-async : any
  [val      : any      := (:val repl)
   repltype : any      := (:type repl)
   repllist : repllist := (:repllist repl)
   args     : anylist  := (anylist<-repllist repllist)]
  (if
   (then (!-empty val) val)
   (then (is-func repltype)
    (any-repl<-functype-args
     repltype
     args))
   (else
    (new repltype args))))
 :async
 :context
 :doc "Run an arbitrary program in the REPL.")
*/

(func any<-script : any
 [script : string]
 (let : any
  [textblock : tb/textblock := (textblock-repl<-script script)
   repl      : repl         := (repl<-textblock textblock)]
  (any<-repl repl))
 :context
 :sideeffects "Since any code can be run. This is unsafe by nature."
 :doc "Run an arbitrary script.")

/*
(func any<-script-async : any
 [text : string]
 (let-async : any
  [textblock : tb/textblock := (textblock-repl<-string text)
   repl      : repl         := (repl<-textblock textblock)]
  (any<-repl-async repl))
 :context
 :async
 :sideeffects "Since any code can be run. This is unsafe by nature."
 :doc "Run an arbitrary script asynchronously.")
*/

(func anylist<-repllist : anylist
 [repllist : repllist]
 (list<-list : anylist
  repllist
  (fn : any
   [repl : repl]
   (any<-repl repl)))
 :context
 :doc "Run an arbitrary program in the REPL.")

(func macro : any-1
 [anylist : anylist :...]
 (let : any-1
  [textlist : stringlist   := (list<-list
                               anylist
                               (fn [item : any]
                                (string<-any item)))
   script   : string       := (type/string<-stringlist-join
                               textlist
                               "")
   tb       : tb/textblock := (textblock-repl<-script
                               script)
   repl     : repl         := (repl<-textblock tb)]
  (any<-any : any-1
   (any<-repl repl)))
 :context
 :doc "A function that joins any number of values into a string and then parses and evaluates it.")

(func repl<-liblist-string : repl
 [liblist : liblist
  text    : string :doc "Program to run"]
 :doc "Returns a repl from the given text.")

(func repl<-textblock : repl
 [textblock : tb/textblock]
 :doc "Returns a parsed repl from a parsed textblock")

(func textblock-repl<-script : tb/textblock
 [script : string]
 (tb/textblock-parse<-string-delim
  script
  delimvxlisp)
 :test (test
        (tb/textblock
         :text "(+ 2 3)"
         :startpos 1
         :endpos 7
         :children
          (tb/textblocklist
           (tb/textblock
            :text "(+ 2 3)"
            :startpos 1
            :endpos 7
            :delim
             (copy delimvxlispparen
              :delimlist
               (empty delimlist))
            :children
             (tb/textblocklist
              (tb/textblock
               :text "+"
               :startpos 2
               :endpos 2)
              (tb/textblock
               :text " "
               :startpos 3
               :endpos 3
               :delim
                (copy tb/delimwhitespace
                 :pos 0))
              (tb/textblock
               :text "2"
               :startpos 4
               :endpos 4)
              (tb/textblock
               :text " "
               :startpos 5
               :endpos 5
               :delim
                (copy tb/delimwhitespace
                 :pos 0))
              (tb/textblock
               :text "3"
               :startpos 6
               :endpos 6)))))
        (textblock-repl<-script
         "(+ 2 3)"))
 :doc "Returns a parsed textblock from a string")
